* PSpice Model Editor - Version 16.0.0	
*$
* UCCx813-3 and UCCx803
*****************************************************************************
** This product is designed as an aid for customers of Texas Instruments.  **
** No warranties, either expressed or implied, with respect to this third  **
** party software (if any) or with respect to its fitness for any          **
** particular purpose is claimed by Texas Instruments or the author. The   **
** software (if any) is provided solely on an "as is" basis. The entire    **
** risk as to its quality and performance is with the customer             **
*****************************************************************************
*
* This model was developed for Texas Instruments Incorporated by:
*   AEi Systems, LLC
*   5777 W. Century Blvd., Suite 876
*   Los Angeles, California  90045
*
* This model is subject to change without notice. Neither Texas Instruments Incorporated 
* nor AEi Systems is responsible for updating this model.
* For more information regarding modeling services, model libraries and simulation 
* products, please call AEi Systems at (310) 216-1144, or contact AEi Systems by email: 
* info@AENG.com. Or visit AEi Systems on the web at http://www.AENG.com.
*
*****************************************************************************
*
* (C) Copyright 2008 Texas Instruments Incorporated. All rights reserved.
* Released by: Analog e-Lab Design Center, Texas Instruments Inc.
* Part: UCCx813-3 and UCCx803
* Date: 06OCT2011
* Model Type: Transient
* Simulator: PSpice
* Simulator Version: 16.0.0.p001
* Application Note: SLUA288 April 2003 
* Datasheet: SLUS161B - April 1999 - Revised April 2008 (UCCx813-3)
*            SLUS270C - March 1999 - Revised January 2005 (UCCx803)   
*
* Model Version: Final 1.11
*
*****************************************************************************
*
* Updates:
*
* Final 1.11
* No change made to model
* Only corrupt simulation profile in PSPICE replaced with a new one
* 
* Final 1.10
* Changed the model for AMP3802 and minor changes to UCC3803 model
* Changed COMP rise time to 4ms
*
* Final 1.00
* Release to web.
*
*****************************************************************************
.SUBCKT UCC3803 COMP FB CS CT GND DRV VCC VREF 
XUV VCC uvlout UVLOX2 PARAMS: VON=4.1 VOFF=3.6 ; UVLO Setting
SX5 ct gnd 52 0 SSWITCH 
.MODEL SSWITCH VSWITCH RON=125 ROFF=10MEG VON=4 VOFF=2
*EBD 26 drv Value= { IF ( V(39) > 2.5 & V(Tog) > 2.5 & V(16) > 2.5 &
*+ V(vref) > 3.8 , 5 , -5 ) }; Driver Setting 95% of Vref, 3.8 or 4.75
EBD 26 gnd Value= { IF ( V(39) > 2.5 & V(Tog) > 2.5 & V(16) > 2.5 &
+ V(vref) > 3.8 , 5 , -5 ) }; Driver Setting 95% of Vref, 3.8 or 4.75
XDRIVE VCC GND 26 drv FETOUTC4X ; FETOUTC4X 2845OUT
*Mtop VCC 26 drv drv _NMOS 
*.MODEL _NMOS NMOS KP=1 RD=10 VTO=2
GBIcc VCC gnd Value= { IF ( V(uvlout) > 3 , (500u+I(VR2)) , 100u ) }
XVccZen gnd VCC ZEN13p5
EBRefOsc 1 gnd Value={ 0.625*V(Vref) } ; Osc Midpoint Setting 50% or 62.5%
EBpul oscout 0 Value= { IF (V(puls)>2 , 5 , 100m ) }
*Mbot gnd 26 drv drv _PMOS 
*.MODEL _PMOS PMOS KP=1 RD=10 VTO=-2
XTog 65 27 Tog OR2380x 
X1 20 ct 11 COMPAR380x 
X3 ct 1 17 COMPAR380x 
Vosc 20 gnd DC=200m
Rdum 0 35 100k
X7 16 ctrst INV380x 
X8 16 17 38 AND2380x 
X6 ctrst 11 6 OR2380x 
Rcs CS gnd 1Meg
EBCSLEB CSLEB gnd Value= { IF (V(LEB) > 3 , V(CS,GND) , 0 ) }
XLEBD 39 LEB UTD380x
RDumm2 43 0 100k
EBComp 44 gnd Value= { IF ((V(comp)-0.78)*0.6 > 1 , 1 , IF ( (V(comp)-0.78)*0.6 < 10m , 10m , (V(comp)-0.78)*0.6 ) ) }
EBEARef 45 gnd Value= { V(vref)/2 }
X13 32 33 34 53 LATCH380x 
X14 36 33 37 56 LATCH380x 
R17 53 0 100k
R18 56 0 100k
X16 57 41 33 AND2380x 
X17 ctimer 59 36 COMPAR380x 
X18 60 ctimer 41 COMPARHYS 
Vssu 59 0 DC=4
Vssl 60 0 DC=500m
X19 32 57 INV380x 
Ctim ctimer 0 80n
Qtim ctimer 61 0 QN380x 
.MODEL QN380x NPN BF=105 BR=4 CJC=12.2P CJE=35.5P IKF=.5
+ IKR=.225 IS=15.2F ISE=8.2P NE=2 NF=1 NR=1 RB=1.49 RC=.149
+ RE=.373 TF=500P TR=85N VAF=98.5 VAR=20 XTB=1.5
Rtim 61 29 10k
Dtim ctimer vref _DCLAMP 
.MODEL _DCLAMP D N=10m
EBctim vss 0 Value= { IF (V(ctimer) < 100m , 100m , V(ctimer) ) }
Vcsleb 64 0 DC=1.5
GBtim vref ctimer Value= { IF ( V(vref) > 3.8 , 80u , 0 ) } ; SS Setting 95% of Vref, 3.8 or 4.75
X20 63 uvlout 32 NAND2380x 
X21 34 16 INV380x 
X12 34 37 29 AND2380x 
RSref 47 vref 360
XROSC ct 66 rstosc COMPAR380x 
Vrstosc 66 gnd DC=2.45
XHD oscout 19 39 43 LATCH_HD 
X29 38 6 3 35 LATCH_HD 
X99DC CSLEB 44 15 COMPAR380x 
Cpuls 3 puls 1n
Rpuls puls 0 100
Dpuls 0 puls _DCLAMP 
X4 64 CSLEB 63 COMPARCS ; COMPARX1 
X10 ctrst 3 52 OR2380x 
XEA 45 FB comp gnd vss AMP3802 
XRstHD 15 rstosc 12 19 OR3 
X0DC 69 comp 12 COMPARX1 
Vpwm 69 gnd DC=780m
XTFF 52 70 0 0 70 27 FFLOP380x 
Drout 46 gnd _D 
.MODEL _D D N=0.1
VTog 65 0 DC=5 ; Set VTog to zero for 49% 01, 04, 05, 5V for 99% 00, 02, 03
Vrout vref 46 DC=3.93 ; Either 3.93 or 4.93
Dreflo uvlout 18 _D7_mod 
.MODEL _D7_mod D BV=100V CJO=4PF IS=1u M=.45 N=0.1 RS=.8
+ TT=6E-09 VJ=.6V
Rreflo 18 vref 4k
R5kpd uvlout gnd 500k
Qreflo gnd uvlout 18 _PNP 
.MODEL _PNP PNP BF=500
VR2 uvlout 47
.ENDS
*$
.SUBCKT UVLOX2  1   2  PARAMS: VON=4.1 VOFF=3.6
*            VIN OUT
S1 1 3 1 0 SUVLO
RUV 3 0 100K
EB1 4 0 Value= { IF ( V(3) > 3V , V(1) , 0 ) }
RD 4 2 100
CD 2 0 100P
.MODEL SUVLO VSWITCH RON=1 ROFF=1E6 VT={((VON-VOFF)/2) + VOFF} VH={(VON-VOFF)/2}
.ENDS 
*$
.SUBCKT UVLO_G 1   2  30 PARAMS: VON=12 VOFF=10
*             VIN OUT Gnd
S1 1 3 1 30 SUVLO
RUV 3 30 100K
EB1 4 0 Value= { IF ( V(3,30) > 5V , 5V , 0 ) }
RD 4 2 100
CD 2 0 100P
.MODEL SUVLO VSWITCH RON=1 ROFF=100E6 VT={((VON-VOFF)/2) + VOFF} VH={(VON-VOFF)/2}
; VON = VT+VH = 15.0 VOFF = VT-VH = 7.6V
.ENDS UVLO_G
*$
.SUBCKT ZEN13p5 1 2
D1 1 2 DZEN
.MODEL DZEN D(RS=7.3771 BV=13.432 CJO=620.01P TT=20N N=2 IS=1E-9 IBV=9.2593M)
.ENDS
*$
.SUBCKT FFLOP380x 1 2 11 12 5 6
*                CLK D R S QBAR Q
X1 7 4 2 8 NAND380x0 
X2 8 3 10 9 NAND380x0 
X3 1 8 10 7 NAND380x1 
X4 4 9 1 10 NAND380x0 
X5 4 7 6 5 NAND380x1 
X6 5 10 3 6 NAND380x0 
X7 11 4 INV380x1 
X8 12 3 INV380x1 
.ENDS
*$
.SUBCKT NAND380x0 1 2 3 4 
EB1 5 0 VALUE = { IF ( (V(1)>1.5V) & (V(2)>1.5V) & (V(3)>1.5V), 0.3, 3.5 ) }
R1 5 4 400
C1 4 0 20P IC=0
.ENDS
*$
.SUBCKT NAND380x1 1 2 3 4 
EB1 5 0 VALUE = { IF ( (V(1)>1.5V) & (V(2)>1.5V) & (V(3)>1.5V), 0.3, 3.5 ) }
R1 5 4 400
C1 4 0 20P IC=3.5
.ENDS
*$
.SUBCKT INV380x1 1 2
EB1 4 0 Value= { IF ( V(1)>2.5 , 0 , 5V ) }
RD 4 2 100
CD 2 0 10P IC={3.5}
.ENDS 
*$
.SUBCKT INV380x 1 2
EB1 4 0 Value= { IF ( V(1)>2.5 , 0 , 5V ) }
RD 4 2 100
CD 2 0 10P
.ENDS 
*$
.SUBCKT OR2380x 1 2 3
EB1 4 0 Value= { IF ( (V(1)>2.5) | (V(2)>2.5) , 5V , 100m ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
.SUBCKT AND2380x 1 2 3
EB1 4 0 Value= { IF ( (V(1)>2.5) & (V(2)>2.5) , 5V , 100m ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
.SUBCKT UTD380x 1 2 
*Parameters K=GAIN TD=DELAY
RIN 1 0 1E15
E1 3 0 1 0 1
T1 3 0 2 0 Z0=1 TD=100n
R1 2 0 1
.ENDS
*$
.SUBCKT LATCH380x 6 8 2 1
*                 S R Q Qb
EBQB   10 0 Value= { IF ((V(8)<2.5) & (V(2)>2.5) , 100m , 5 ) }
EBQ    20 0 Value= { IF ((V(6)<2.5) & (V(1)>2.5) , 100m , 5 ) }
RD1   10 1 100
CD1   1 0 10p IC=5
RD2   20 2 100
CD2   2 0 10p IC=100m
.ENDS 
*$
.SUBCKT COMPARHYS NINV INV OUT PARAMS: VHIGH=5 VLOW=100m VHYS=50m
EB2 HYS NINV Value= { IF ( V(OUT) > (VHIGH+VLOW)/2 , VHYS , 0 ) }
EB1 4 0 Value= { IF ( V(HYS,INV) > 0 , VHIGH , VLOW ) }
RO 4 OUT 100
CO OUT 0 100PF
.ENDS
*$
.SUBCKT NAND2380x 1 2 3
EB1 4 0 Value= { IF ( (V(1)>2.5) & (V(2)>2.5) , 100m , 5V ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
.SUBCKT LATCH_HD S R Q QN PARAMS: VHIGH=5 VLOW=100m
X1 Sin R Q QN LMlatch380x
X2 S 6 Sin AND2BLM 
X3 R 6 INVBLM
.ENDS
*$
.SUBCKT LMlatch380x set reset Q Qbar
EB1 5 0 Value={ IF ( v(set) + v(Q) < .5 , 5 , 0 ) } 
EB2 7 0 Value={ IF ( (v(reset) + v(Qbar)) *(1- v(set) )< .5 , 5 , 0 ) }
R1 set 0 1g
R2 reset 0 1g
R3 5 Qbar 1
C1 Qbar 0 1n ; 10p
R4 7 Q 1
C2 Q 0 1n ; 10p
.ENDS
*$
.SUBCKT AND2BLM 1 2 3
* A B Out
EB1 4 0 VALUE = { IF ( V(1)>800m & V(2) > 800m , 5, 0 ) }
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
.SUBCKT INVBLM 1 2
* in out
E1 3 0 VALUE = { IF ( V(1)>800mV, 0, 5 ) }
RD 3 2 1
CD 2 0 0.087NF
.ENDS
*$
.SUBCKT AMP3802 20 8  3  21  SS
*               +  - OUT GND VSS
.PARAM ISINK=3m
.PARAM ISOURCE=500u
.PARAM VLOW=10m
.PARAM POLE=200
.PARAM GAIN=10k
RIN 20 8 8MEG
CP1 11 21 {1/(6.28*(GAIN/100U)*POLE)}
E1 OUT 21 11 21 1
Rfil2 OUT OUT1 100
Cfil2 OUT1 0 318p
Rfil3 OUT1 OUT2 50
Cfil3 OUT2 0 100p
R9 OUT2 2 5
D14 2 13 DMOD
IS 13 21 {ISINK/100} ; mA
Q1 21 13 16 QPMOD
ISRC 7 3 {ISOURCE}   ; uA
D12 3 7 DMOD
D15 21 11 DCLAMP
G1 21 11 20 8 100U
EB1 7 21 Value = { V(SS)-0.7 }
V4 3 16 {VLOW-38MV}
RP1 11 21 {GAIN/100U}
.MODEL QPMOD PNP
.MODEL DCLAMP D (RS=10 BV=10 IBV=0.01)
.MODEL DMOD D (TT=1N CJO=10P)
.ENDS
*$
.SUBCKT OR3 1 2 3 5
EB1 4 0 Value= { IF ( (V(1)>2.5) | (V(2)>2.5) | (V(3)>2.5) , 5V , 100m ) }
RD 4 5 100
CD 5 0 10P
.ENDS 
*$
.SUBCKT COMPARCS NINV INV OUT PARAMS: VHIGH=5 VLOW=100m
EB1 4 0 Value= { IF ( V(NINV,INV) > 0 , VHIGH , VLOW ) }
RO 4 OUT 100
CO OUT 0 1NF
.ENDS
*$
.SUBCKT COMPAR380x NINV INV OUT PARAMS: VHIGH=5 VLOW=100m
EB1 4 0 Value= { IF ( V(NINV,INV) > 0 , VHIGH , VLOW ) }
RO 4 OUT 100
CO OUT 0 100PF
.ENDS
*$
.SUBCKT COMPARX1 NINV INV OUT PARAMS: VHIGH=5 VLOW=100m
EB1 4 0 Value= { IF ( V(NINV,INV) > 0 , VHIGH , VLOW ) }
RO 4 OUT 100
CO OUT 0 10PF
.ENDS
*$
.MODEL QN2907A PNP (IS=51.6f NF=1.00 BF=367 VAF=139
+ IKF=0.213 ISE=8.95p NE=2.00 BR=4.00 NR=1.00
+ VAR=20.0 IKR=0.450 RE=0.123 RB=0.492 RC=49.2m
+ XTB=1.5 CJE=41.5p VJE=1.10 MJE=0.500 CJC=23.1p VJC=0.300
+ MJC=0.300 TF=764p TR=30.0n EG=1.12 )
*$
.MODEL QN2222A NPN (IS=56.7f NF=1.00 BF=367 VAF=156
+ IKF=0.213 ISE=9.38p NE=2.00 BR=4.00 NR=1.00
+ VAR=24.0 IKR=0.450 RE=0.102 RB=0.410 RC=41.0m
+ XTB=1.5 CJE=30.2p VJE=1.10 MJE=0.500 CJC=23.1p VJC=0.300
+ MJC=0.300 TF=464p TR=84.4n EG=1.12 )
*$
.MODEL ES1A D IS=3.6033E-6 N=2.8597 RS=3.8597E-3 IKF=.87204 BV=50.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=74.32n
*$
.MODEL ES1B D IS=3.6033E-6 N=2.8597 RS=3.8597E-3 IKF=.87204 BV=100.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=74.32n
*$
.MODEL ES1C D IS=3.6033E-6 N=2.8597 RS=3.8597E-3 IKF=.87204 BV=150.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=74.32n
*$
.MODEL ES1D D IS=3.6033E-6 N=2.8597 RS=3.8597E-3 IKF=.87204 BV=200.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=74.32n
*$
.MODEL ES1G D IS=5.3465E-6 N=3.0620 RS=5.5383E-3 IKF=52.158E-3 BV=400.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=108n
*$
.MODEL ES1J D IS=5.3465E-6 N=3.0620 RS=5.5383E-3 IKF=52.158E-3 BV=600.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=108n
*$
.MODEL ES1K D IS=97.197E-6 N=5 RS=35.831E-3 IKF=.1195 BV=800.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=108n
*$
.MODEL ES1M D IS=97.197E-6 N=5 RS=35.831E-3 IKF=.1195 BV=1000.0 IBV=1.00u
+ CJO=202.08E-12 VJ=.3905 M=.84796 TT=144n
*$
.MODEL MA2ZD18 D BV=20 CJO=100E-12 IBV=70E-6
+ IS=1.85u M=0.3333 N=0.99709 RS=0.11739 TT=10.1E-9 VJ=.75
+ IKF=211.04 ISR=11.864E-6 NR=4.9950
*$
.SUBCKT Si7344DP   10 20 30
*     TERMINALS:  D  G  S
M1   1  2  3  3  DMOS L=1U W=1U
RD  10  1  3.51m
RS  40  3  1.24m
RG  20  2  0.7
CGS  2  3  1.15n
EGD 12  0  2  1  1
VFB 14  0  0
FFB  2  1  VFB  1
CGD 13 14  1.40n
R1  13  0  1.00
D1  12 13  DLIM
DDG 15 14  DCGD
R2  12 15  1.00
D2  15  0  DLIM
DSD  3 10  DSUB
LS  30 40  2.50n
.MODEL DMOS NMOS(LEVEL=3 VMAX=41.7k THETA=80.0m
+ ETA=2.00m VTO=1.50 KP=48
.MODEL DCGD D (CJO=1.40n VJ=0.600 M=0.680
.MODEL DSUB D (IS=58.1n N=1 RS=0.1 BV=20.0
+ CJO=743p VJ=0.38 M=0.25 TT=50n
.MODEL DLIM D (IS=100U)
.ENDS
*$
.SUBCKT XFMR 1 2 3 4 PARAMS: RATIO=1
* SINGLE WINDING TRANSFORMER 
Rpar 1 2 1MEG
Ea 5 4 VALUE = { V(1,2)*RATIO }
Ga 1 2 VALUE = { I(VMa)*RATIO }
Rser 6 3 1U
VMa 5 6 
RP2 5 0 100Meg
RP3 6 0 100Meg
.ENDS XFMR
*$
.MODEL D D
*$
.SUBCKT IRF520    9  60 14
*     TERMINALS:  D  G  S
M1 9 6 14 14 _M520 
.MODEL _M520 NMOS KP=10 RD=0.1 RS=0.1 VTO=3.3
RG 60 6 1
C5 6 14 300p
C6 6 9 30p
C7 9 14 20p
DSD 14 9 DSUB
.MODEL DSUB D (N=1 RS=0.1)
.ENDS
*$
.SUBCKT IRF830  Drain Gate Source
M1 Drain 2 7 7 _IRF830 
.MODEL _IRF830 NMOS KP=1.022 LAMBDA=2.5m RD=1.1 VTO=3.818
CGS 2 7 550p
Rb 2 Gate 2.2
R2 2 Source 10meg
D2 3 Drain _CRSS 
.MODEL _CRSS D CJO=1.1n M=0.672 VJ=0.795
D3 3 2 _D3_modX 
.MODEL _D3_modX D CJO=4n M=0.9 VJ=0.795
Dbody 7 Drain _D1_mod 
.MODEL _D1_mod D BV=550 CJO=750p IBV=250u IS=6.523n M=0.684
+ N=1.5 RS=17m TT=320n VJ=0.75
L1 7 Source 7n
.ENDS
*$
.model D1N752	D(Is=1.154f Rs=.9471 Ikf=0 N=1 Xti=3 Eg=1.11 Cjo=150p M=.5788
+		Vj=.75 Fc=.5 Isr=1.625n Nr=2 Bv=5.6 Ibv=62.583m Nbv=.62382
+		Ibvl=631.96u Nbvl=50 Tbv1=267.86u)
*		Motorola	pid=1N752	case=DO-35
*		89-9-18 gjg
*		Vz = 5.6 @ 20mA, Zz = 40 @ 1mA, Zz = 4.5 @ 5mA, Zz = 1.9 @ 20mA
*$
.model D1N4148  D(Is=2.682n N=1.836 Rs=.5664 Ikf=44.17m Xti=3 Eg=1.11 Cjo=4p
+               M=.3333 Vj=.5 Fc=.5 Isr=1.565n Nr=2 Bv=100 Ibv=100u Tt=11.54n)
*$
.MODEL MBR140P D
+ is= 4.41547e-06 rs= 0.103922 n= 1.03751 tt= 1e-12
+ cjo= 1.598e-10 vj= 0.4934 m= 0.4258 eg= 0.6
+ xti= 3.29768 fc= 0.5 bv= 48 ibv= 0.01 kf= 0 af= 1
*$
.SUBCKT MTD1N60E  100 20 30
*     TERMINALS:  D  G  S
LDRAIN  100  10  4.5e-09
M1   1  2  3  3  DMOS L=1U W=1U
RD  10  1  2.80
RS  40  3  0.148
RG  20  2  150
CGS  2  3  218p
EGD 12  0  2  1  1
VFB 14  0  0
FFB  2  1  VFB  1
CGD 13 14  77.0p
R1  13  0  1.00
D1  12 13  DLIM
DDG 15 14  DCGD
R2  12 15  1.00
D2  15  0  DLIM
DSD  3 10  DSUB
LS  30 40  7.50n
.MODEL DMOS  NMOS(LEVEL=1 LAMBDA=2.00m VTO=3.30 KP=0.896
.MODEL DCGD D (CJO=77.0p VJ=0.600 M=0.680
.MODEL DSUB D (IS=4.15n N=1.50 RS=70.0m BV=635
+ CJO=120.3p VJ=0.800 M=0.420 TT=464n
.MODEL DLIM D (IS=100U)
.ENDS
*$
.SUBCKT XFMR2 1 2 3 4 10 11 PARAMS: RATIO1=1 RATIO2=1
* MULTI-WINDING TRANSFORMER
RP 1 2 1MEG
Ea 5 4 VALUE = { V(1,2)*RATIO1 }
Ga 1 2 VALUE = { I(VMA)*RATIO1 }
RSe1 6 3 1U
VMA 5 6
Eb 20 11 VALUE = { V(2,1)*RATIO2 }
Gb 2 1 VALUE = { I(VMB)*RATIO2 }
RSe2 21 10 1U
VMB 20 21 
.ENDS XFMR2
*$
.SUBCKT MOC8101 1 2 3   5
* ISOLATOR      A C COL EMITTER
RB 4 0 100Meg
VM 1 6
D1 6 2 LED
H1 7 0 VM .0055
R1 7 8 1K
C1 8 0 3.35nF
G1 3 4 8 0 1
Q1 3 4 5 MPSA06
.MODEL LED D(N=1.7 RS=.7 CJO=23.9P IS=85.3p BV=6 IBV=10U 
+ VJ=0.75 M=0.333 TT=4.32U)
.MODEL MPSA06 NPN (IS=15.2F NF=1 BF=589 VAF=98.6 IKF=90M ISE=3.34P NE=2 
+ BR=4 NR=1 VAR=16 IKR=0.135 RE=0.343 RB=1.37 RC=0.137 XTB=1.5
+ CJE=9.67P VJE=1.1 MJE=0.5 CJC=7.34P VJC=0.3 MJC=0.3 TF=10.29n TR=276N)
.ENDS MOC8101
*$
.SUBCKT FETOUTC4X  Vcc  Gnd In Out
*                  Vcc  -V  IN OUT
* simplify with b element with 5.5 ohms make it stiff vcc and zero
Rm1 Vcc Out 10MEG
M1 Vcc 3 Out Out _NMOS 
.MODEL _NMOS NMOS KP=1 NSUB=1E9 RD=10 VTO=3
Rm2 Out Gnd 10MEG
M2 Gnd 3 Out Out _PMOS 
.MODEL _PMOS PMOS KP=1 RD=5.5 VTO=-3 NSUB=1E9
EB1 2 Out Value= { IF ( V(In,Gnd) > 2 , 5 , -5 ) }
R1 2 3 200
C2 3 Gnd 10p
C3 3 Vcc 10p
R2 3 Gnd 10Meg
.ENDS FETOUTC4X 
*$

